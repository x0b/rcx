apply plugin: 'com.android.application'

android {
    signingConfigs {
        github_x0b {
            keyAlias 'github_x0b'
        }
    }
    compileSdkVersion 30
    ndkVersion '21.3.6528147'
    defaultConfig {
        applicationId 'io.github.x0b.rcx'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 200 // last digit is reserved for ABI, only ever end on 0!
        versionName '1.12.0'
        resConfigs "en", "de" // restrict to used languages
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "java.lang.String", "CLI", System.getenv('RCX_BUILD_CLI') ? System.getenv('RCX_BUILD_CLI') : "\"f49658d8-2b3f-a284-6789-767e89b3bbc8\""
        buildConfigField "java.lang.String", "VCP_AUTHORITY", "\"" + applicationId + ".vcp\"";
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            buildConfigField "java.lang.long", "BUILD_TIME", "0L"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.lang.long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L"
        }
    }

    sourceSets {
        all {
            jniLibs.srcDirs = ["lib"]
        }
    }

    flavorDimensions 'edition'
    productFlavors {
        // Default GitHub and Google Play build
        rcx {
            dimension 'edition'
        }
        // OSS build
        oss {
            dimension 'edition'
            buildConfigField "java.lang.long", "BUILD_TIME", "0L"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    project.ext.versionCodes = [
            'armeabi-v7a': 6,
            'arm64-v8a'  : 7,
            'x86'        : 8,
            'x86_64'     : 9]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + android.defaultConfig.versionCode
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion = '29.0.3'
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.leinardi.android:speed-dial:2.0.0'
    implementation 'org.markdownj:markdownj-core:0.4'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.github.GrenderG:Toasty:1.3.0'
    // Appcenter (sourced from own sdk fork to ensure control)
    def appCenterSdkVersion = '4.0.0'
    implementation "com.github.x0b.appcenter-sdk-android:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.github.x0b.appcenter-sdk-android:appcenter-crashes:${appCenterSdkVersion}"
    // Thumbnails
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    // REST Client
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.1'
    // Timestmps
    implementation 'com.github.x0b:rfc3339parser:2.0.0'
    // SAF/WebDAV
    implementation project(':safdav')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    // Java 8+ library support
    // TODO: replace with coreLibraryDesugar once available
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.3'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.3'
    // App intro
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    // TEST
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
